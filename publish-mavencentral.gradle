apply plugin: 'maven-publish'
apply plugin: 'signing'

if (PUBLISH_TYPE == "java") {
//    task javadocJar(type:Jar) {
//
//    }
//    task sourcesJar(type:Jar) {
//        classifier = 'sources'
////        from android.sourceSets.main.java.source
//    }
//    if (someMagicFlag) {
//        javadocJar.enabled = false
//        sourcesJar.enabled = false
//    }
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
} else {
    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.source
    }
    artifacts {
        archives androidSourcesJar
    }
}

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
}

publishing {
    publications {
        release(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION

            artifact(PUBLISH_TYPE == "java" ? "$buildDir/libs/${project.getName()}.jar" : "$buildDir/outputs/aar/${project.getName()}-release.aar")
            if (PUBLISH_TYPE == "android") artifact androidSourcesJar
            else {
                artifact sourcesJar
                artifact javadocJar
            }
            pom {
                name = PUBLISH_ARTIFACT_ID
                description = '项目描述'
                url = 'https://github.com/kouchengjian/androidx_utils'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kouchengjian'
                        name = 'kouchengjian'
                        email = 'hui793114465@163.com'
                    }
                }
                scm {
                    connection = 'scm:git:github.com/kouchengjian/androidx_utils.git'
                    developerConnection = 'scm:git:ssh://github.com/kouchengjian/androidx_utils.git'
                    url = 'https://github.com/kouchengjian/androidx_utils/tree/master'
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "Releases"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = PUBLISH_VERSION.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}
signing {
    sign publishing.publications
}
